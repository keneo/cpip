buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath group: "net.carlosgsouza", name: "gradle-console", version: "1.+"
    classpath 'de.undercouch:gradle-download-task:3.2.0'
  }
}

apply plugin: 'de.undercouch.download'

import de.undercouch.gradle.tasks.download.Download
import de.undercouch.gradle.tasks.download.Verify

apply plugin: "java"
apply plugin: "application"
apply plugin: "checkstyle"
apply plugin: "console"

repositories {
  mavenCentral()
  flatDir {
    //dirs 'libs/geotools-17.0'
    dirs 'libs/auto/geotools-17.0-bin/geotools-17.0'
  }
  //maven { name = "osgeo"; url = "http://download.osgeo.org/webdav/geotools/" }
  // I tried to use maven repo - but it has a well known issue that is requires jai_core-1.1.3 lib to be installed manually
  // and its was really hard to find how to install it manually
}

dependencies {
  testCompile group: "junit", name: "junit", version: "4.+"
  //compile 'javax.media:jai_core:1.1.3'
  //compile name: 'jai_core-1.1.3'
  //compile 'org.geotools:gt-shapefile:17.0'
  compile group: "org.geotools", name: "gt-shapefile", version: "17.0"
  compile group: "org.geotools", name: "gt-main", version: "17.0"
  compile group: "org.geotools", name: "gt-api", version: "17.0"
  compile group: "org.geotools", name: "gt-opengis", version: "17.0"
  compile group: "org.geotools", name: "jts", version: "1.13"
}

mainClassName = "Main"


task downloadGeotoolsLibraryZipFile(type: Download) {
  src 'https://netix.dl.sourceforge.net/project/geotools/GeoTools%2017%20Releases/17.0/geotools-17.0-bin.zip'
  dest new File('libs/auto/geotools-17.0-bin.zip')
  overwrite false
}

task verifyGeotoolsLibraryZipFile(type: Verify, dependsOn: downloadGeotoolsLibraryZipFile) {
  src new File('libs/auto/geotools-17.0-bin.zip')
  algorithm 'MD5'
  checksum '3b0ae66ba42a3861f64fd7756f1f5bb4'
}

task unzipGeotoolsLibraryZip(dependsOn: verifyGeotoolsLibraryZipFile, type: Copy) {
  from zipTree(downloadGeotoolsLibraryZipFile.dest)
  into 'libs/auto/geotools-17.0-bin'
}

task downloadCountriesFile(type: Download) {
  src 'http://www.naturalearthdata.com/http//www.naturalearthdata.com/download/110m/cultural/ne_110m_admin_0_countries.zip'
  dest new File(buildDir, 'ne_110m_admin_0_countries.zip')
  overwrite false
}

task verifyCountriesFile(type: Verify, dependsOn: downloadCountriesFile) {
  src new File(buildDir, 'ne_110m_admin_0_countries.zip')
  algorithm 'MD5'
  checksum 'ef4f89c7732a16ba2a1986c9e043303d'
}

task downloadCountriesFileAndUnzip(dependsOn: verifyCountriesFile, type: Copy) {
  from zipTree(downloadCountriesFile.dest)
  into buildDir
}

compileJava.dependsOn downloadCountriesFileAndUnzip
compileJava.dependsOn unzipGeotoolsLibraryZip

